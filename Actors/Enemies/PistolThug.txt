ACTOR PistolThug: actor 
{
  Health 30
  Radius 15
  Height 58
  mass 80
  Speed 8
  species human
  PainChance 256
  Monster
  +FLOORCLIP
  +NOINFIGHTSPECIES
  +missilemore
  +avoidmelee
  scale 0.8
  Decal "BulletChip"
  SeeSound "vulture/sight"
  PainSound "goon/pain"
  DeathSound "goon/death"
  Obituary "%o was shot down by a Vulture Thug."
  DropItem "Autoloaderspawn"
  DropItem "NineMilAmmoDropper"
  DropItem "FALSpawn"
  States
  {
  Spawn:
    VUL1 V 10 A_Look
    Loop
  //Switching
  See:
    VUL1 A 0 A_AlertMonsters(150)
    VUL1 A 0 A_ChangeFlag("MissileEvenMore", 0)
	VUL1 A 0 A_TakeInventory("GoonRally", 1)
	VUL1 A 0 A_TakeInventory("GoonWimp", 1)
	VUL1 A 0 A_TakeInventory("GoonScared", 1)
	VUL1 A 0 A_JumpIfHealthLower(5, "ScaredSwitch")
	VUL1 A 0 A_JumpIfInventory("GoonFear", 15, "ScaredSwitch")
    VUL1 A 0 A_JumpIfInventory("GoonOrder", 1, "WanderSwitch")
	//Normal Mode
	VUL1 AABBCCDD 2 A_Chase
	VUL1 A 0 A_checksight("ambushsee")
	VUL1 A 0 A_CheckProximity("AggressiveSwitch","pistolthug",256.0,2,CPXF_ANCESTOR)
	loop
	 //Aggressive Mode
  AggressiveSwitch:
    VUL1 A 0 A_ChangeFlag("MissileEvenMore", 1)
    Goto AggressiveSee
  AggressiveSee:
	VUL1 AABBCCDD 2 A_Chase
	VUL1 A 0 A_JumpIfHealthLower(10, "See")
	VUL1 A 0 A_checksight("see")
    goto see
  //Search Mode
  WanderSwitch:
    VUL1 A 0 A_ClearTarget
    Goto WanderSee
	WanderSee:
	VUL1 A 0 A_jump(240,2)
	VUL1 A 0 A_PlaySound("vulture/search",CHAN_VOICE)
    VUL1 A 3 A_Wander
    VUL1 A 0 A_LookEx(10, 0, 0, 0, 360, "See")
    VUL1 B 3 A_Wander
    VUL1 A 0 A_LookEx(10, 0, 0, 0, 360, "See")
	VUL1 C 3 A_Wander
    VUL1 A 0 A_LookEx(10, 0, 0, 0, 360, "See")
	VUL1 D 3 A_Wander
    VUL1 A 0 A_LookEx(10, 0, 0, 0, 360, "See")
	VUL1 A 0 A_jump(16,"idle")
	goto ambushsee
	//Ambush mode
	AmbushSee:
	VUL1 A 0 A_jump(246,2)
	VUL1 A 0 A_PlaySound("vulture/search",CHAN_VOICE)
	VUL1 AABBCCD 2 A_Chase
	VUL1 D 2 A_jump(32,"waitsee")
    goto see
	Waitsee: 
	VUL1 E 10 A_sentinelrefire
	VUL1 E 0 A_jump(32,"Ambushsee")
	VUL1 E 0 A_checksight("waitsee")
	goto missile+1
   //Scared Mode
   Scaredswitch:
    VUL1 A 0 A_ChangeFlag("Frightened", 1)
    Goto ScaredSee
	Scaredsee:
	VUL1 A 0 A_jump(246,2)
	VUL1 A 0 A_PlaySound("Vulture/run",CHAN_VOICE)
	VUL1 ABCD 3 A_Chase
	VUL1 D 0 A_checksight("idlereset")
	goto see
  IdleReset:
    VUL1 E 0 A_facetarget
    VUL1 E 0 A_cleartarget
   Idle:
    VUL1 E 10 A_Look
    Loop
  Missile:
    VUL1 W 10 A_FaceTarget
	VUL1 E 0 A_FaceTarget
    VUL1 E 10 Fast
	VUL1 E 0 A_FaceTarget
	VUL1 F 0 A_PlaySound("weapons/glock",CHAN_WEAPON)
	VUL1 F 0 A_PlaySound("Distant/pistol",CHAN_BODY)
	VUL1 F 0 A_AlertMonsters
    VUL1 F 5 bright  A_CustomMissile("BulletTracerSmall", 32, 12, random(-9, 9), CMF_OFFSETPITCH, random(-1, 1))
	VUL1 E 10 A_facetarget
	VUL1 F 0 A_PlaySound("weapons/glock",CHAN_WEAPON)
	VUL1 F 5 bright  A_customMissile("BulletTracerSmall", 32, 12, random(-9, 9), CMF_OFFSETPITCH, random(-1, 1))
	VUL1 F 0 A_AlertMonsters
	VUL1 E 5
    Goto see
  Pain:
    VUL1 G 4
    VUL1 H 4 A_Pain
    Goto See
  BURN:                
    VUL1 G 1 A_PlaySound("special/firedeath", CHAN_VOICE)
	POSS A 0 A_radiusgive("goonfear",512,0,8)
    POSS A 0 A_HideThing
    POSS A 0 A_Fall
    POSS A 0 A_SpawnItem("BurnDeathHuman",1,0,0)
    POSS A -1
    Stop
  Death:
    VUL1 G 4 A_Scream
    VUL1 H 5 A_radiusgive("goonfear",512,0,4)
    VUL1 I 4 A_AlertMonsters(150)
    VUL1 J 4 A_NoBlocking
    VUL1 K 3
    VUL1 L -1
    Stop
  XDeath.fire:
  XDeath:
    VUL1 M 3 A_PlaySound("Special/Gibdeath", CHAN_BODY)
	VUL1 M 3 A_PlaySound("goon/explode", CHAN_VOICE)
	VUL1 NNNN 0 A_SpawnDebris("FlyingBlood",1)
    VUL1 N 4 A_SpawnDebris("FlyingGibOffal",1)
	VUL1 NN 0 A_SpawnDebris("FlyingBlood",1)
	VUL1 N 0 A_SpawnDebris("FlyingGibArm",1)
	VUL1 N 0 A_SpawnDebris("FlyingGibEntrails",1)
	VUL1 N 0 A_SpawnDebris("FlyingGibRib",1)
    VUL1 O 4 A_NoBlocking
	VUL1 NNN 0 A_SpawnDebris("FlyingBlood",1)
    VUL1 PQRST 4
    VUL1 U -1
    Stop
  }
}

Actor ThugTalkative: PistolThug replaces MarinePistol
{
States
  {
    Spawn:
    VUL1 V 10 A_turretlook
    Loop
}
}

Actor ThugTalkative3: PistolThug replaces MarineFist
{
States
  {
    Spawn:
    VUL1 V 10 A_turretlook
    Loop
}
}
Actor DeadThug : PistolThug replaces Deadzombieman 
{
		Skip_Super
		DropItem "None"
		scale 0.75
	States
	{
	Spawn:
		Goto Super::Death+4
	}
}
Actor GibbedThug : PistolThug replaces Gibbedmarine
{
    Skip_Super
	DropItem "None"
	States
	{
	Spawn:
		Goto Super::XDeath+4
	}
}